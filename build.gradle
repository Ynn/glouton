buildscript {
    ext {
        kotlin_version = '1.1.60'
        vertx_version = '3.5.0'
        ktor_version = '0.4.0'
        sl4j_version = '1.7.25'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}


apply plugin: "kotlin"

group = "nnynn"
sourceCompatibility = '1.8'
mainClassName = 'io.vertx.core.Launcher'
def mainVerticleName = 'nny.glouton.MainVerticle'


repositories {
    jcenter()
    mavenCentral()
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}


dependencies {
    compile 'io.github.microutils:kotlin-logging:1.4.6'
    compile "org.slf4j:slf4j-api:$sl4j_version"
    compile "org.slf4j:slf4j-jdk14:$sl4j_version"

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-runtime:$kotlin_version"

    testCompile group: 'junit', name: 'junit', version: '3.8.2'
    compile group: 'org.codehaus.woodstox', name: 'woodstox-core-asl', version: '4.4.1'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml', version: '2.9.2'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.9.2'

    compile "io.vertx:vertx-core:$vertx_version"
    compile "io.vertx:vertx-lang-kotlin:$vertx_version"
    compile "io.vertx:vertx-web:$vertx_version"
    compile "io.vertx:vertx-mqtt:$vertx_version"
    compile "io.vertx:vertx-web-client:$vertx_version"
    compile "io.vertx:vertx-jdbc-client:$vertx_version"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.0"

    compile group: 'org.mnode.ical4j', name: 'ical4j', version: '2.1.1'
    compile group: 'org.ehcache', name: 'ehcache', version: '3.4.0'

    compile group: 'com.google.guava', name: 'guava', version: '23.0'
    compile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.20.1'

    compile 'org.influxdb:influxdb-java:2.7'
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.12'

    compile group: 'com.opencsv', name: 'opencsv', version: '4.1'

}

// Redeploy watcher.
run {
    args = ['run', mainVerticleName,
            "--launcher-class=$mainClassName",
            "--redeploy=src/**/*.*",
            "--on-redeploy=./gradlew classes"
    ]
}

// Naming and packaging settings for the "shadow jar".
shadowJar {
    baseName = 'glouton'
    classifier = 'all'

    manifest {
        attributes 'Main-Verticle': mainVerticleName
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3.1'
}

// Heroku relies on the 'stage' task to deploy.
task stage {
    dependsOn shadowJar
}
